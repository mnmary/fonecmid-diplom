#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
		
	Если ОбменДанными.Загрузка Тогда
     	Возврат;
	КонецЕсли;
	
	Если ГрафикУжеСуществует() Тогда
		
		ТекстСообщения = СтрШаблон("Запись не возможна. На %1 год график уже существует.", Формат(Год,"ДФ=yyyy;"));
	    ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Год",,Отказ);
		Возврат;
				
	КонецЕсли;

КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, Режим)
		 	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	BKM_ГрафикОтпусковОтпускаСотрудников.Сотрудник КАК Сотрудник,
		|	СУММА(РАЗНОСТЬДАТ(BKM_ГрафикОтпусковОтпускаСотрудников.ДатаНачала, BKM_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания, ДЕНЬ) + 1) КАК ПродолжительностьОтпуска
		|ИЗ
		|	Документ.BKM_ГрафикОтпусков.ОтпускаСотрудников КАК BKM_ГрафикОтпусковОтпускаСотрудников
		|ГДЕ
		|	BKM_ГрафикОтпусковОтпускаСотрудников.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	BKM_ГрафикОтпусковОтпускаСотрудников.Сотрудник";
	

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();	
	
	// регистр BKM_СведенияОбОтпусках 
	Движения.ВКМ_СведенияОбОтпусках.Записывать = Истина;
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_СведенияОбОтпусках.Добавить();
		Движение.Период = Дата;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Год = Год;
		Движение.ПлановаяПродолжительностьОтпуска = Выборка.ПродолжительностьОтпуска;
		
	КонецЦикла;

	Движения.ВКМ_СведенияОбОтпусках.Записать();
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ГрафикУжеСуществует()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	BKM_ГрафикОтпусков.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.BKM_ГрафикОтпусков КАК BKM_ГрафикОтпусков
		|ГДЕ
		|	BKM_ГрафикОтпусков.Год = &Год
		|	И BKM_ГрафикОтпусков.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Год", Год);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Следующий();

КонецФункции

#КонецОбласти

#КонецЕсли