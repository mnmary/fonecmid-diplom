#Область ОбработчикиСобытийЭлементовТаблицыФормыОтпускаСотрудников

&НаКлиенте
Процедура ОтпускаСотрудниковПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОтпускаСотрудниковПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьСотрудниками(Команда) 
	
	Объект.ОтпускаСотрудников.Очистить();
	ЗаполнитьСотрудникамиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура АнализГрафика(Команда)
	
	ПараметрыФормы = Новый Структура;	
	ПараметрыФормы.Вставить("Адрес", ПоместитьДанныеВоВременноеХранилище()); 
	
	ОткрытьФорму("Документ.BKM_ГрафикОтпусков.Форма.ФормаАнализаГрафика", ПараметрыФормы, ЭтотОбъект);   	
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтпускаСотрудниковПриИзмененииНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписокОтпусков.Сотрудник КАК Сотрудник,
		|	СписокОтпусков.НомерСтроки КАК НомерСтроки,
		|	РАЗНОСТЬДАТ(СписокОтпусков.ДатаНачала, СписокОтпусков.ДатаОкончания, ДЕНЬ) + 1 КАК ПродолжительностьОтпуска
		|ПОМЕСТИТЬ ВТ_Список
		|ИЗ
		|	&СписокОтпусков КАК СписокОтпусков
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Список.Сотрудник КАК Сотрудник,
		|	СУММА(ВТ_Список.ПродолжительностьОтпуска) КАК ОбщаяПродолжительностьОтпуска
		|ПОМЕСТИТЬ ВТ_Список1
		|ИЗ
		|	ВТ_Список КАК ВТ_Список
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Список.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Список.НомерСтроки,
		|	ВТ_Список.ПродолжительностьОтпуска,
		|	ВТ_Список1.ОбщаяПродолжительностьОтпуска,
		|	ВТ_Список.Сотрудник
		|ИЗ
		|	ВТ_Список КАК ВТ_Список
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Список1
		|		ПО ВТ_Список.Сотрудник = ВТ_Список1.Сотрудник";
	
	
	СписокОтпусков = Объект.ОтпускаСотрудников.Выгрузить(,"Сотрудник,НомерСтроки,ДатаНачала,ДатаОкончания" );
	Запрос.УстановитьПараметр("СписокОтпусков",СписокОтпусков);
	
	ТаблицаОтпусков = Запрос.Выполнить().Выбрать();   
	
		
	Пока ТаблицаОтпусков.Следующий() Цикл
		
		Объект.ОтпускаСотрудников[ТаблицаОтпусков.НомерСтроки - 1].ПродолжительностьОтпуска = ТаблицаОтпусков.ПродолжительностьОтпуска;
		Объект.ОтпускаСотрудников[ТаблицаОтпусков.НомерСтроки - 1].ОбщееКоличествоДнейОтпуска = ТаблицаОтпусков.ОбщаяПродолжительностьОтпуска;
		
	КонецЦикла;
	
КонецПроцедуры


&НаСервере                                       
Функция ПоместитьДанныеВоВременноеХранилище() 
	
	Возврат ПоместитьВоВременноеХранилище(Объект.ОтпускаСотрудников.Выгрузить(), УникальныйИдентификатор);  
	
КонецФункции 

&НаСервере
Процедура ЗаполнитьСотрудникамиНаСервере() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	BKM_Сотрудники.Ссылка КАК Сотрудник
		|ИЗ
		|	Справочник.BKM_Сотрудники КАК BKM_Сотрудники
		|ГДЕ
		|	НЕ BKM_Сотрудники.ПометкаУдаления";
	
	СписокСотрудников = Запрос.Выполнить().Выгрузить();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(СписокСотрудников,Объект.ОтпускаСотрудников);
	
КонецПроцедуры


#КонецОбласти
