#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//Создание списка документов Реализации товаров и услуг за указанный месяц.
//
// Возвращаемое значение:
//   Структура: 
//   * ДокументовСоздано - Число
//   * ДокументовНеСоздано - Число
//   * МассивДоговоровИРеализаций - Массив из Структура
//   
// Параметры:  
//     ДатаНачалаПериода - Дата - Дата начала месяца
//     ДатаОкончанияПериода - Дата - Дата конца месяца 
Функция СоздатьСписокДокументовРеализацииТоваровИУслуг(Знач ДатаНачалаПериода, Знач ДатаОкончанияПериода) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	ДоговорыКонтрагентов.Владелец КАК Владелец,
		|	ДоговорыКонтрагентов.Организация КАК Организация,
		|	ПРЕДСТАВЛЕНИЕ(ДоговорыКонтрагентов.Владелец) КАК КонтрагентПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДоговорыКонтрагентов.Ссылка) КАК ДоговорПредставление
		|ПОМЕСТИТЬ ВТ_ДействующиеДоговора
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.BKM_АбонентскоеОбслуживание)
		|	И НЕ ДоговорыКонтрагентов.ПометкаУдаления
		|	И ДоговорыКонтрагентов.BKM_ДатаНачалаДействияДоговора <= &ДатаОкончанияПериода
		|	И ДоговорыКонтрагентов.BKM_ДатаОкончанияДействияДоговора >= &ДатаНачалаПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка КАК Документ
		|ПОМЕСТИТЬ ВТ_СуществующиеДокументы
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.Проведен
		|	И РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачалаПериода И &ДатаОкончанияПериода
		|	И РеализацияТоваровУслуг.Договор В
		|			(ВЫБРАТЬ
		|				ВТ_ДействующиеДоговора.Договор КАК Договор
		|			ИЗ
		|				ВТ_ДействующиеДоговора КАК ВТ_ДействующиеДоговора)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДействующиеДоговора.Договор КАК Договор,
		|	ЕСТЬNULL(ВТ_СуществующиеДокументы.Документ, ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)) КАК Документ,
		|	&ДатаОкончанияПериода КАК Дата,
		|	""Документ создан автоматически."" КАК Комментарий,
		|	ВТ_ДействующиеДоговора.Владелец КАК Контрагент,
		|	ВТ_ДействующиеДоговора.Организация КАК Организация,
		|	ВТ_ДействующиеДоговора.ДоговорПредставление КАК ДоговорПредставление,
		|	ВТ_ДействующиеДоговора.КонтрагентПредставление КАК КонтрагентПредставление
		|ИЗ
		|	ВТ_ДействующиеДоговора КАК ВТ_ДействующиеДоговора
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуществующиеДокументы КАК ВТ_СуществующиеДокументы
		|		ПО ВТ_ДействующиеДоговора.Договор = ВТ_СуществующиеДокументы.Документ.Договор
		|ГДЕ
		|	ЕСТЬNULL(ВТ_СуществующиеДокументы.Документ, ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)) = ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ДатаНачалаПериода", ДатаНачалаПериода);
	Запрос.УстановитьПараметр("ДатаОкончанияПериода", ДатаОкончанияПериода);

		
	Выборка = Запрос.Выполнить().Выбрать();
	
	//Накопление результатов
	ДокументовСоздано = 0;
	ДокументовНеСоздано = 0; 
	
	СписокДоговоровИРеализаций = Новый ТаблицаЗначений;
	СписокДоговоровИРеализаций.Колонки.Добавить("Договор", Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	СписокДоговоровИРеализаций.Колонки.Добавить("Реализация", Новый ОписаниеТипов("ДокументСсылка.РеализацияТоваровУслуг"));	
	
	Пока Выборка.Следующий() Цикл                            
		
		ДокументРеализации = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		ДокументРеализации.Заполнить(Выборка);
		ДокументРеализации.BKM_ВыполнитьАвтозаполнение();
		
		// Не создавать документы с пустыми табличными частями. 
		// Это может быть в случае нулевой абонентской платы и отсутствия работ специалиста 	
		Если ДокументРеализации.СуммаДокумента = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка 
			Если ДокументРеализации.ПроверитьЗаполнение() Тогда 
				
				ДокументРеализации.Записать(РежимЗаписиДокумента.Проведение); 
				
				ДокументовСоздано = ДокументовСоздано + 1;

				Строка = СписокДоговоровИРеализаций.Добавить();
				Строка.Договор = Выборка.Договор;
				Строка.Реализация = ДокументРеализации.Ссылка;
										
			Иначе 
          
				ТекстИсключения = СтрШаблон("Обнаружены ошибки при проверке заполнения.
									|Документ Реализации товаров и услуг для контрагента %1 с договором %2 не создан.",Выборка.КонтрагентПредставление, Выборка.ДоговорПредставление);
				ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации(ТекстИсключения, УровеньЖурналаРегистрации.Ошибка,,, "Документ не создан.");
				ВызватьИсключение ТекстИсключения;
				
			КонецЕсли;
			
		Исключение  
			
			ДокументовНеСоздано = ДокументовНеСоздано + 1;
			ЗаписьЖурналаРегистрации("НедопустимаяОперация", УровеньЖурналаРегистрации.Ошибка,,,ОписаниеОшибки());
			
		КонецПопытки;
		
		
	КонецЦикла;
	
	Результат = Новый Структура();
	Результат.Вставить("МассивДоговоровИРеализаций", ОбщегоНазначения.ТаблицаЗначенийВМассив(СписокДоговоровИРеализаций));
	Результат.Вставить("ДокументовСоздано",ДокументовСоздано);
	Результат.Вставить("ДокументовНеСоздано", ДокументовНеСоздано);
	
	Возврат Результат;
	
КонецФункции 

#КонецОбласти

#КонецЕсли 
